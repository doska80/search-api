buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://repo.spring.io/release" }
        maven { url "http://repo.spring.io/milestone" }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:+'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:' + SPRING_BOOT_VERSION
        classpath 'gradle.plugin.com.avast.gradle:gradle-docker-compose-plugin:+'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.+'
        classpath 'com.github.ben-manes:gradle-versions-plugin:+'
        classpath 'com.gradle:build-scan-plugin:2.+'
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE'
        classpath 'me.champeau.gradle:jmh-gradle-plugin:0.4.8'
        classpath 'org.owasp:dependency-check-gradle:+'
        classpath 'com.datadoghq:dd-trace-api:0.25.+'
    }
}

apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'com.diffplug.gradle.spotless'
apply plugin: 'com.gradle.build-scan'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'
apply plugin: 'docker-compose'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.spring.dependency-management'
apply plugin: "me.champeau.gradle.jmh"

group = 'com.grupozap.search'

ext {
    artifactName = project.hasProperty('artifactName') ? artifactName : 'grupozap-search-api-SNAPSHOT.jar'
    set('elasticsearch.version', ELASTICSEARCH_VERSION)
}

eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = false
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = false
    }
}

sourceCompatibility = 11
targetCompatibility = 11

jacoco { toolVersion = "0.8.2" }

spotless {
    java {
        googleJavaFormat('1.7')
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'grupozap Search API v2', 'Implementation-Version': artifactName
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://repo.spring.io/release" }
    maven { url "http://repo.spring.io/milestone" }
    jcenter()
}

configurations {
    compile.exclude module: 'spring-boot-starter-tomcat'
    compile.exclude module: 'undertow-websockets-jsr'
    compile.exclude module: 'hibernate-validator'
    datadog
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:' + SPRING_CLOUD_VERSION
        mavenBom 'org.springframework.boot:spring-boot-dependencies:' + SPRING_BOOT_VERSION
    }
}

dependencies {
    // java >= 9 modules compatibility
    compile 'javax.xml.bind:jaxb-api:2.+'

    compile 'org.springframework.cloud:spring-cloud-starter-netflix-hystrix'
    compile 'org.springframework.cloud:spring-cloud-starter-netflix-hystrix-dashboard'
    compile 'org.springframework.cloud:spring-cloud-starter-netflix-turbine'

    compile 'org.springframework.boot:spring-boot-starter-undertow'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-data-elasticsearch'

    compile 'io.micrometer:micrometer-core'
    compile 'io.micrometer:micrometer-registry-prometheus'

    compile 'org.elasticsearch:elasticsearch'
    compile 'org.elasticsearch.client:elasticsearch-rest-high-level-client'

    compile 'com.getsentry.raven:raven-logback:8.0.3'
    compile 'net.logstash.logback:logstash-logback-encoder:5.2'

    compile 'io.springfox:springfox-swagger2:' + SWAGGER
    compile 'io.springfox:springfox-swagger-ui:' + SWAGGER

    compile 'org.apache.commons:commons-lang3:3.+'
    compile 'org.apache.commons:commons-collections4:4.+'

    compile 'com.google.code.gson:gson:2.8.5'

    compile 'org.codehaus.janino:janino:3.1.0'

    compile 'org.jparsec:jparsec:3.1'

    compile 'com.datadoghq:dd-trace-api:0.25.+'
    
    datadog 'com.datadoghq:dd-java-agent:' + DATADOG_VERSION

    // elasticsearch/lucene unit tests
    testCompile 'org.elasticsearch.test:framework:' + ELASTICSEARCH_VERSION

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    // Fixtures
    testCompile 'br.com.six2six:fixture-factory:3.1.0'
    testCompile 'com.jayway.restassured:rest-assured:2.9.0'
}

bootRun {
    systemProperties = System.properties
    jvmArgs = ["-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005"]
}

task datadogDeps(dependsOn: 'bootJar') {
    doLast {
        copy {
            from configurations.datadog
            into './build/libs'
            rename('dd-java-agent-' + DATADOG_VERSION + '.jar', 'datadog.jar')
        }
    }
}

task copyJarForDockerCompose(dependsOn: 'datadogDeps', type: Copy) {
    from bootJar
    into './build/libs/'
    rename { 'search-api.jar' }
}

bootJar {
    baseName = artifactName - '.jar'
    launchScript()
}

bootJar.finalizedBy copyJarForDockerCompose

sonarqube {
    properties {
        property "sonar.projectKey", "com.grupozap:search-api"
        property "sonar.projectName", "search-api"
        property "sonar.scm.provider", "git"
        property "sonar.host.url", "http://sonarqube.vivareal.io/"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.login", "9eb3df2e079962a62dd40c39a9430795c856280e"
        property "sonar.sources", sourceSets.main.allSource.srcDirs
        property "sonar.tests", [sourceSets.test.java.srcDirs, 'src/integration-test/java'].flatten()
    }
}

test {
    testLogging {
        events 'skipped', 'failed'
        showStackTraces true
        exceptionFormat 'full'
    }
}

/****************************
 * Healthcheck functions
 ****************************/

def isSearchApiHealthy = {
    return isDependencyHealthy('http://localhost:8482/actuator/health')
}

def isDependencyHealthy(String url) {
    try {
        url.toURL().text
        return true
    } catch (e) {
        return false
    }
}

def waitForTestDependencyHealthy(name, seconds, healthcheck) {
    println "Waiting until $name becomes healthy"
    while(!healthcheck() && seconds >= 0) {
        sleep(1 * 1000)
        println "Waiting another " + (seconds--) + " seconds to $name becomes healthy"
    }
}

/****************************
 * Integration tests config
 ****************************/

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations{
    integrationTestCompile.extendsFrom testCompile
    integrationTestCompile.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoIntegrationTest.exec")
    }
}

test.finalizedBy(integrationTest)

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

/****************************
 * Docker compose config
 ****************************/

if(!project.hasProperty('noDockerCompose')) {
    dockerCompose {
        useComposeFiles = ["${project.projectDir.toString()}/src/integration-test/resources/docker/docker-compose.yml"]
        stopContainers = true
        removeContainers = true
        removeVolumes = true
        waitForTcpPorts = false

        environment.put 'ELASTICSEARCH_VERSION', ELASTICSEARCH_VERSION
    }

    dockerCompose.isRequiredBy(integrationTest)

    composeUp.doLast {
        waitForTestDependencyHealthy('searchApi', 120, isSearchApiHealthy)
    }
}

jmh {
    iterations = 1
    fork = 1
    threads = 4
    batchSize = 1
    timeUnit = 'us'           // Output time unit. Available time units are: [m, s, ms, us, ns].

    verbosity = 'NORMAL'      // Verbosity mode. Available modes are: [SILENT, NORMAL, EXTRA]
    benchmarkMode = ['all']   // Benchmark mode. Available modes are: [Throughput/thrpt, AverageTime/avgt, SampleTime/sample, SingleShotTime/ss, All/all]
    include = ['.*Benchmark']
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishAlways()
}
