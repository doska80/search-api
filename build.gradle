buildscript {
    ext {
        springBootVersion = '2.0.0.M3'
        integrationTestComposeMode = project.hasProperty('circleCi') ? 'circleci' : 'localhost'
        integrationTestComposeFile = "${project.projectDir.toString()}/src/integration-test/resources/docker/docker-compose-${integrationTestComposeMode}.yml"
    }

    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://repo.spring.io/release" }
        maven { url "http://repo.spring.io/milestone" }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "com.avast.gradle:docker-compose-gradle-plugin:0.5.0"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.5"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath 'com.gradle:build-scan-plugin:1.9'
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE'
    }
}

apply plugin: 'com.gradle.build-scan'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'
apply plugin: 'docker-compose'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.spring.dependency-management'

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'

    publishAlways()
}

group = 'com.vivareal.search'

ext {
    artifactName = project.hasProperty('artifactName') ? artifactName : 'vivareal-search-api-v2-SNAPSHOT.jar'
}

eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = false
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = false
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'VivaReal Search API v2', 'Implementation-Version': artifactName
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://repo.spring.io/release" }
    maven { url "http://repo.spring.io/milestone" }
    mavenLocal()
}

configurations {
    compile.exclude module: 'spring-boot-starter-tomcat'
    compile.exclude module: 'spring-boot-starter-logging'
    compile.exclude module: 'undertow-websockets-jsr'
    compile.exclude module: 'hibernate-validator'
    newRelic
}

dependencyManagement {
    dependencies {
        dependencySet(group:'org.springframework.cloud', version: '2.0.0.M2') {
            entry 'spring-cloud-starter-hystrix'
            entry 'spring-cloud-starter-hystrix-dashboard'
            entry 'spring-cloud-netflix-turbine'
        }
    }
}

dependencies {
    compile 'com.netflix.servo:servo-core:0.12.17'

    compile 'org.springframework.cloud:spring-cloud-starter-hystrix'
    compile 'org.springframework.cloud:spring-cloud-starter-hystrix-dashboard'
    compile 'org.springframework.cloud:spring-cloud-netflix-turbine'

    compile 'org.springframework.boot:spring-boot-starter-undertow'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-data-elasticsearch'

    compile 'com.getsentry.raven:raven-logback:8.0.3'
    compile 'biz.paluch.logging:logstash-gelf:1.11.1'

    compile 'io.springfox:springfox-swagger2:' + SWAGGER
    compile 'io.springfox:springfox-swagger-ui:' + SWAGGER

    compile 'org.jparsec:jparsec:3.0'

    compile 'org.glowroot:glowroot-agent-api:0.9.24'
    compile 'com.newrelic.agent.java:newrelic-api:' + NEW_RELIC_VERSION
    newRelic 'com.newrelic.agent.java:newrelic-agent:' + NEW_RELIC_VERSION

    // elasticsearch/lucene unit tests
    testCompile 'org.apache.lucene:lucene-test-framework:' + LUCENE_VERSION
    testCompile 'org.elasticsearch.test:framework:' + ELASTICSEARCH_VERSION
    testCompile 'org.elasticsearch:elasticsearch:' + ELASTICSEARCH_VERSION
    testCompile 'org.elasticsearch.client:elasticsearch-rest-high-level-client:' + ELASTICSEARCH_VERSION

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    // Fixtures
    testCompile 'br.com.six2six:fixture-factory:3.1.0'
    testCompile 'com.jayway.restassured:rest-assured:2.9.0'

}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
    distributionType = DistributionType.ALL
}

bootRun {
    systemProperties = System.properties
}

task newrelicDeps(dependsOn: 'bootJar') {
    doLast {
        copy {
            from configurations.newRelic.fileCollection { it.name == 'newrelic-agent' }.singleFile
            into './build/libs'
            rename { 'newrelic.jar' }
        }
    }

    if (project.hasProperty('newRelicKey')) {
        doLast {
            copy {
                filter(ReplaceTokens, tokens: [newRelicKey: newRelicKey])
                from 'src/main/resources/newrelic.yml'
                into './build/resources/main'
            }
        }
    }
}

task copyJarForDockerCompose(dependsOn: 'newrelicDeps', type: Copy) {
    from bootJar
    into './build/libs/'
    rename { 'search-api.jar' }
}

bootJar {
    baseName = artifactName - '.jar'
    launchScript {
        included = true
    }
}

bootJar.finalizedBy copyJarForDockerCompose

sonarqube {
    properties {
        property "sonar.projectKey", "br.com.vivareal:search-api-v2"
        property "sonar.projectName", "search-api-v2"
        property "sonar.scm.provider", "git"
        property "sonar.host.url", "http://sonarqube.vivareal.io/"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.login", "9eb3df2e079962a62dd40c39a9430795c856280e"
        property "sonar.sources", sourceSets.main.allSource.srcDirs
        property "sonar.tests", [sourceSets.test.java.srcDirs, 'src/integration-test/java'].flatten()
    }
}

project(":load-test") {
    sonarqube {
        skipProject = true
    }
}

test {
    testLogging {
        events 'skipped', 'failed'
        showStackTraces true
        exceptionFormat 'full'
    }
}

/****************************
 * Healthcheck functions
 ****************************/

def isSearchApiHealthy = {
    return isDependencyHealthy('http://localhost:8482/application/health')
}

def isESHealthy = {
    return isDependencyHealthy('http://localhost:9200/_cluster/health')
}


def isDependencyHealthy(String url) {
    try {
        url.toURL().text
        return true
    } catch (e) {
        return false
    }
}

def waitForTestDependencyHealthy(name, seconds, healthcheck) {
    println "Waiting until $name becomes healthy"
    while(!healthcheck() && seconds >= 0) {
        sleep(1 * 1000)
        println "Waiting another " + (seconds--) + " seconds to $name becomes healthy"
    }
}

/****************************
 * Integration tests config
 ****************************/

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations{
    integrationTestCompile.extendsFrom testCompile
    integrationTestCompile.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    doFirst {
        if(!isESHealthy() && !isSearchApiHealthy()) {
            throw new RuntimeException("Searchapi compose is not healthy");
        }
    }

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

test.finalizedBy(integrationTest)

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

/****************************
 * Docker compose config
 ****************************/

if(!project.hasProperty('noDockerCompose')) {

    dockerCompose {
        useComposeFiles = [integrationTestComposeFile]
        stopContainers = true
        removeContainers = false
        removeVolumes = false
        waitForTcpPorts = false

        environment.put 'ELASTICSEARCH_VERSION', ELASTICSEARCH_VERSION
    }

    dockerCompose.isRequiredBy(integrationTest)


    composeUp.doFirst {
        println "Using: $integrationTestComposeFile"
    }

    composeUp.doLast {
        waitForTestDependencyHealthy('es', 180, isESHealthy)
        waitForTestDependencyHealthy('searchApi', 120, isSearchApiHealthy)
    }
}
